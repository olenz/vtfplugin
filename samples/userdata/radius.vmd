# EXAMPLE VTFTOOLS USERDATA SCRIPT
# Read in a vtf file that has changing radii for each atom in each
# timestep. Sets the radii for all atoms in each timestep and also
# sets the beta value to the radius.

package require vtftools

# this is the callback that is called upon a change in the frame
proc change_radius_callback { name molid op } {
    global vmd_frame radius_update_sel
    set pattern "$molid.step$vmd_frame($molid)."
    set radii {}
    $radius_update_sel update
    foreach pid [ $radius_update_sel list ] {
	lappend radii $::VTFTools::userdata($pattern$pid)
    }

    $radius_update_sel set radius $radii
    $radius_update_sel set beta $radii
}

proc sample_load { filename } {
    global radius_update_sel

    # load the vtf file
    vtf_load $filename
    
    # set the molecule id
    set molid [ molinfo "top" ]

    # create the selection
    set radius_update_sel [ uplevel atomselect $molid "all" ]

    # setup the trace
    uplevel trace add variable vmd_frame($molid) write change_radius_callback

    # call it once
    change_radius_callback {} $molid {}

    return $molid
}

set molid [sample_load radius.vtf]

mol modstyle 0 $molid vdw
mol modcolor 0 $molid beta

pbc box

